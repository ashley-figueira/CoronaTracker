// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        jcenter()
        
    }
    dependencies {
        classpath Libs.gradle_plugin
        classpath Libs.kotlin_plugin
        classpath Libs.androidx_nav_safe_args_plugin
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                compileSdkVersion Versions.compile_sdk

                defaultConfig {
                    minSdkVersion Versions.min_sdk
                    targetSdkVersion Versions.target_sdk
                    versionCode Versions.app_version_code
                    versionName Versions.app_version_name
                    vectorDrawables.useSupportLibrary true
                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

                    javaCompileOptions {
                        annotationProcessorOptions {
                            arguments = [
                                "room.incremental":"true"
                            ]
                        }
                    }

                }

                flavorDimensions "default"
                productFlavors {
                    dev {}
                    prod {}
                }

                buildTypes {
                    debug {
                        debuggable true
                        minifyEnabled false
                        testCoverageEnabled true
                    }
                    release {
                        testCoverageEnabled false
                        debuggable false
                    }
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }

                kotlinOptions {
                    jvmTarget = JavaVersion.VERSION_1_8.toString()
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
